#
# SPDX-License-Identifier: GPL-3.0-or-later
# myGPIOd (c) 2020-2023 Juergen Mang <mail@jcgames.de>
# https://github.com/jcorporation/myGPIOd
#
# Maintainer: Juergen Mang <mail@jcgames.de>
#


pkgname=mygpiod
_pkgname=myGPIOd
pkgver=__VERSION__
pkgrel=1
pkgdesc="A small daemon to call scripts on GPIO events. "
arch=('x86_64' 'armv7h' 'aarch64')
url="https://jcorporation.github.io/myGPIOd/"
license=('GPL')
depends=('libgpiod')
makedepends=('cmake')
optdepends=()
provides=()
conflicts=()
replaces=()
backup=('etc/mygpiod.conf')
install=archlinux.install
source=("mygpiod_${pkgver}.orig.tar.gz")
sha256sums=('SKIP')
options=(debug strip)

build() {
  local cmake_options=(
    -B build
    -S "${srcdir}"
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DMYGPIOD_STRIP_BINARY=OFF
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 "${srcdir}"/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
