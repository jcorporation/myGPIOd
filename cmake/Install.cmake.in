# SPDX-License-Identifier: GPL-3.0-or-later
# myMPD (c) 2020-2024 Juergen Mang <mail@jcgames.de>
# https://github.com/jcorporation/mygpiod

# Install startup script
function(install_systemd)
  file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod.service"
       DESTINATION "/usr/lib/systemd/system"
       FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
endfunction()

function(install_openrc)
  file(RENAME "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod.openrc"
       "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod")
  file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod"
       DESTINATION "/etc/init.d"
       FILE_PERMISSIONS OWNER_WRITE OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
endfunction()

function(install_sysvinit)
  file(RENAME "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod.sysVinit"
       "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod")
  file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/initscripts/mygpiod"
       DESTINATION "/etc/init.d"
       FILE_PERMISSIONS OWNER_WRITE OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
endfunction()

if("@MYGPIOD_DAEMON@" STREQUAL "ON")
  if("@MYGPIOD_STARTUP_SCRIPT@" STREQUAL "ON")
    # Try to detect installed init system
    if(EXISTS "/lib/systemd/system" OR EXISTS "/usr/lib/systemd/system")
      install_systemd()
    elseif(EXISTS "/sbin/openrc-run")
      install_openrc()
    elseif(EXISTS "/etc/init.d")
      install_sysvinit()
    else()
      message("No supported init system found, no startup script was installed")
    endif()
  elseif("@MYGPIOD_STARTUP_SCRIPT@" STREQUAL "SYSTEMD")
    install_systemd()
  elseif("@MYGPIOD_STARTUP_SCRIPT@" STREQUAL "OPENRC")
    install_openrc()
  elseif("@MYGPIOD_STARTUP_SCRIPT@" STREQUAL "SYSVINIT")
    install_sysvinit()
  endif()

  # Install myGPIOd configuration
  if(NOT EXISTS "$ENV{DESTDIR}@CMAKE_INSTALL_FULL_SYSCONFDIR@/mygpiod.conf")
    file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/etc/mygpiod.conf"
        DESTINATION @CMAKE_INSTALL_FULL_SYSCONFDIR@
        FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
  else()
    file(RENAME "@CMAKE_CURRENT_BINARY_DIR@/contrib/etc/mygpiod.conf"
                "@CMAKE_CURRENT_BINARY_DIR@/contrib/etc/mygpiod.conf.dist")
    file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/etc/mygpiod.conf.dist"
        DESTINATION @CMAKE_INSTALL_FULL_SYSCONFDIR@
        FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
  endif()

  # Install example gpio configuration files
  file(INSTALL "@CMAKE_SOURCE_DIR@/contrib/etc/gpio-in.example"
      DESTINATION "@CMAKE_INSTALL_FULL_SYSCONFDIR@/mygpiod.d"
      FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
  file(INSTALL "@CMAKE_SOURCE_DIR@/contrib/etc/gpio-out.example"
      DESTINATION "@CMAKE_INSTALL_FULL_SYSCONFDIR@/mygpiod.d"
      FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
endif()

# Install and compress manpages
if("@MYGPIOD_MANPAGES@" STREQUAL "ON")
  if("@MYGPIOD_DAEMON@" STREQUAL "ON")
    execute_process(COMMAND gzip -n -c -9 "@CMAKE_SOURCE_DIR@/contrib/man/man1/mygpiod.1"
                    OUTPUT_FILE "@CMAKE_CURRENT_BINARY_DIR@/contrib/mygpiod.1.gz")
    file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/mygpiod.1.gz"
        DESTINATION "@CMAKE_INSTALL_FULL_MANDIR@/man1"
        FILE_PERMISSIONS PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
  endif()

  if("@MYGPIOD_CLIENT@" STREQUAL "ON")
    execute_process(COMMAND gzip -n -c -9 "@CMAKE_SOURCE_DIR@/contrib/man/man1/mygpioc.1"
                    OUTPUT_FILE "@CMAKE_CURRENT_BINARY_DIR@/contrib/mygpioc.1.gz")
    file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/mygpioc.1.gz"
        DESTINATION "@CMAKE_INSTALL_FULL_MANDIR@/man1"
        FILE_PERMISSIONS PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
  endif()

  if("@MYGPIOD_LIBRARY@" STREQUAL "ON")
    file(MAKE_DIRECTORY "@CMAKE_CURRENT_BINARY_DIR@/contrib/man3")
    file(GLOB files "@CMAKE_SOURCE_DIR@/contrib/man/man3/*.3")
    foreach(file ${files})
      get_filename_component(filename "${file}" NAME)
      execute_process(COMMAND gzip -n -c -9 "@CMAKE_SOURCE_DIR@/contrib/man/man3/${filename}"
                      OUTPUT_FILE "@CMAKE_CURRENT_BINARY_DIR@/contrib/man3/${filename}.gz")
      file(INSTALL "@CMAKE_CURRENT_BINARY_DIR@/contrib/man3/${filename}.gz"
          DESTINATION "@CMAKE_INSTALL_FULL_MANDIR@/man3"
          FILE_PERMISSIONS PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
    endforeach()
  endif()
endif()

# Install documentation
if("@MYGPIOD_DOC@" STREQUAL "ON")
  file(INSTALL "@CMAKE_SOURCE_DIR@/CHANGELOG.md"
               "@CMAKE_SOURCE_DIR@/LICENSE.md"
               "@CMAKE_SOURCE_DIR@/README.md"
               "@CMAKE_SOURCE_DIR@/PROTOCOL.md"
       DESTINATION "@CMAKE_INSTALL_FULL_DOCDIR@"
       FILE_PERMISSIONS PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ)
endif()
